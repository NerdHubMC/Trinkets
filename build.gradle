plugins {
    id "fabric-loom" version "0.2.0-SNAPSHOT"
    id "com.matthewprenger.cursegradle" version "1.2.0"
    id "maven-publish"
}

sourceCompatibility = targetCompatibility = JavaVersion.VERSION_1_8

group = "com.github.NerdHubMC"
archivesBaseName = "Trinkets"
version = System.getenv("TRAVIS_TAG") ?: "dev"

apply from: "https://raw.githubusercontent.com/NerdHubMC/Gradle-Scripts/master/scripts/fabric/basic_project.gradle"

minecraft {
}

dependencies {
    minecraft "com.mojang:minecraft:${minecraft_version}"
    mappings "net.fabricmc:yarn:${yarn_mappings}"
    modCompile "net.fabricmc:fabric-loader:${loader_version}"
    modCompile "net.fabricmc:fabric:${fabric_version}"

    modCompile "com.github.NerdHubMC:TextileLib:${textile_version}"

    modCompile "io.github.prospector.modmenu:ModMenu:1.1.6-32"
}

repositories {
    mavenLocal()
    maven {
        name = "NerdHub"
        url = "https://maven.abusedmaster.xyz/"
    }
}

processResources {
    // this will ensure that this task is re-run when there's a change
    inputs.property "version", project.version

    // replace stuff in fabric.mod.json, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'fabric.mod.json'

        // add mod metadata
        filter {
            String line -> line.replace("\"version\": \".+\"", "\"version\": \"${project.version}\"")
        }

        filter { String line ->
            line.replace("\"fabric\": \"*\"", "\"fabric\": \"[${project.fabric_version},)\"")
        }
    }

    // copy everything else, thats not the fabric.mod.json
    from(sourceSets.main.resources.srcDirs) {
        exclude 'fabric.mod.json'
    }
}

if(project.hasProperty("curseforge_id")) {
    curseforge.curseProjects.each {
        if(it.id == project.findProject("curseforge_id")) {
            it.relations {
                requiredDependency "fabric"
                requiredDependency "textilelib"
            }
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            pom {
                name = "Trinkets"
                url = "https://github.com/NerdHubMC/Trinkets"
                licenses {
                    license {
                        name = "MIT License"
                        url = "https://github.com/NerdHubMC/Trinkets/blob/master/LICENSE.md"
                    }
                }
                scm {
                    url = "https://github.com/NerdHubMC/Trinkets"
                    connection = "scm:git:git://github.com/NerdHubMC/Trinkets.git"
                    developerConnection = "scm:git:git@github.com:NerdHubMC/Trinkets.git"
                }
                issueManagement {
                    system = "github"
                    url = "https://github.com/NerdHubMC/Trinkets/issues"
                }
            }
        }
    }
}
